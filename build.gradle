plugins {
    id 'java'
  id 'com.diffplug.spotless' version '6.25.0'
}

allprojects {
  group = 'com.codingmonster.matchingengine'
  version = '1.0.0'

  repositories {
    mavenCentral()
  }
}

subprojects {
  apply plugin: 'java'

  java {
    toolchain {
      languageVersion = JavaLanguageVersion.of(21)
    }
  }

    test {
        useJUnitPlatform() // Very important to enable JUnit Jupiter
    }

    def aeronDir = System.getProperty('user.home') + '/aeron'
    plugins.withId('application') {
    applicationDefaultJvmArgs = [
      "--add-opens=java.base/sun.nio.ch=ALL-UNNAMED",
      "-Daeron.dir=" + aeronDir
    ]
  }

  dependencies {
    // Aeron (high-performance messaging)
    implementation 'io.aeron:aeron-all:1.44.0'

     // SBE runtime (required for encoding/decoding)
    implementation 'uk.co.real-logic:sbe-all:1.27.0'

      // high performance data structures
    implementation 'org.agrona:agrona:1.18.0'

      // for reading yaml file
      implementation 'com.fasterxml.jackson.core:jackson-databind:2.17.1'
      implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.17.1'

      // logging
    implementation 'org.slf4j:slf4j-api:2.0.9'
    implementation 'org.slf4j:slf4j-simple:2.0.9'

      // JUnit
      testImplementation platform('org.junit:junit-bom:5.10.0') // or latest version
      testImplementation 'org.junit.jupiter:junit-jupiter'
  }
}

spotless {

    java {
        googleJavaFormat() // or eclipse(), prettier(), etc.
        target '**/*.java'
        removeUnusedImports()
        trimTrailingWhitespace()
        endWithNewline()
    }

    format 'xml', {
        target '**/*.xml'
                        eclipseWtp('xml').configFile('xml-formatter.xml')
        trimTrailingWhitespace()
        endWithNewline()
    }

    format 'yaml', {
        target '**/*.yaml', '**/*.yml'
        prettier().config([
            'parser': 'yaml',
            'tabWidth': 2
        ])
        trimTrailingWhitespace()
        endWithNewline()
    }

    format 'properties', {
        target '**/*.properties'
        trimTrailingWhitespace()
        endWithNewline()
    }

    format 'groovyGradle', {
        target '*.gradle', 'gradle/**/*.gradle'
        trimTrailingWhitespace()
        endWithNewline()
    }
}
